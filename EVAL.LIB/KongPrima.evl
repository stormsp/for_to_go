; Галеев 02.02.15
; 
;для Использования:
;#INCLUDE "eval.lib\ValTrack.evl"
;#INCLUDE "eval.lib\KongPrima.evl"


; -----------------------------------------
; Приведение точки росы к давлению 3.92 МПа по ГОСТ Р 53763-2009
; p_abs - измеренное АБСОЛЮТНОЕ давление в МПа
; t - измеренная точка росы
; -----------------------------------------


func trosy(p_abs,t)

 if(!dost(p_abs)|!dost(t))
  return(false(0))
 endif
 tpr=false(0)

 p=p_abs                ; вычисляем абсолютное давление в МПА для подстановки в формулу 
 ppr=3.92268            ; Давление к которому нужно привести

 if(ge(p,0.1)&le(p,2))
  a=((t+273.15)^-1)+0.000165153*ln(p)-0.004648
  w=exp(a/-0.000161)
 endif

 if(gt(p,2)&le(p,10))
  a=23.9-1.7464*p+0.02592*p*p
  b=(753.06*p-3811.7)/(t+273.15)
  c=(111635*p+267960)/((t+273.15)^2)
  w=exp(a+b-c)
 endif


 if(ge(ppr,0.1)&le(ppr,2))
  a=-0.000161*ln(w)-0.000165153*ln(ppr)+0.004648
  tpr=1/a-273.15
 endif

 if(gt(ppr,2)&le(ppr,10))
  a=1/(-0.0001956*ln(ppr)+0.004647)
  b=(-0.1495*ppr+6.938)*ln(w)
  c=0.4316*(ppr^0.28)*((ln(w))^2)
  tpr=a+b+c-273.15
 endif

 return(tpr)
endfunc


;--- Достоверность точки росы  ---
; при выполнении замера Конг-Прима выдает недостоверные значения
; на экране ПУ это выражается периодическим появлением "звездочек"
; Поэтому пропускаем парметры через вычислитель
; Если параметр недостоверен и связь с контроллером есть, то ждем 20 минут 
; если достоверные показания не появились отображаем недостоверность

; выходных переменных - 2
; id - номер первой выходной переменной
; mbtr - проверяемый параметр, полученный по модбасу
; devCon - наличие связи с Устройством


func dstTrosy(id, mbtr, devCon)

  if dost(mbtr)
	aout[id]=mbtr
  endif

  if(devCon)
    if ValTrack(!dost(mbtr),1200,id+1)
      aout[id]=mbtr
    endif
  else 
    aout[id]=false(0)
  endif
 	return(0)
endfunc






