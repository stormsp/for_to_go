; Расчет объемов QY (за прошлые сутки), QD (с начала суток), за прошлый месяц для sevc и SF

; ********** Функция вычисления qd, qy 
; t      - время устройства на этом шаге
; vs_sys - vsum, параметр непрерывный расход (исходный для всех расчетов)
; qf_sys - fix, параметр, где фиксируется непрер накопленный расход 
;          при смене контр часа (уст извне, чтобы хранить)
; qy_sys - sys параметра qy (уст извне, чтобы хранить)
; qd_ind - индекс расчетной переменной qd
; qmax   - максимальный возможный расход за сутки, больше - недост
; chour  - контрактный час
; aout[vi+0] - расход с начала суток
; aout[vi+1] - для слежения за изменением времени
;
func upd_qyqd(t,vs_sys,qf_sys,qy_sys,vi,qmax,chour) 
  if (ne(hour(t),hour(aout[vi+1]))&eq(hour(t),chour)) ; наступил контр час, в sevc местное время
    qy=#[vs_sys]-#[qf_sys]
    if (lt(qy,0)|gt(qy,qmax))			   ; значение qy некорректно
      qy=false(qy)
    endif
    set qy_sys,qy				   ; qy текущий накоп - накоп на момент послед сут
    set qf_sys,#[vs_sys]                           ; накоп на момент послед сут запоминаем в zond.tmp
    sleep(2)                    		   ; будем польз новым знач QF_SYS
  endif
  aout[vi]=#[vs_sys]-#[qf_sys];            	   ; расход c начала суток
  aout[vi+1]=t
endfunc


; ********** Функция вычисления qm - расхода за месяц
; t      - время устройства на этом шаге
; vs_sys - vsum, параметр непрерывный расход (исходный для всех расчетов)
; qf_sys - fix, параметр, где фиксируется непрер накопленный расход 
;          на начало месяца (уст извне, чтобы хранить)
; qm_sys - sys параметра qy (уст извне, чтобы хранить)
; vi     - индекс переменной для расчетов
; qmax   - максимальный возможный расход за месяц, больше - недост
; chour  - контрактный час
; dout[vi+0] - для управления расчетом
; aout[vi+1] - для слежения за изменением времени
;
func upd_qmes(t,vs_sys,qf_sys,qm_sys,vi,qmax,chour) 
  if (ne(month(t),month(aout[vi+1]))) 	   	   	; наступил новый месяц
    dout[vi]=1						; включаем отложенный расчет qm
  endif
  if (eq(dout[vi],1)&ne(hour(t),hour(aout[vi+1]))&eq(hour(t),chour)) ; расчет включен и наступил к ч
    qm=#[vs_sys]-#[qf_sys]
    if (lt(qm,0)|gt(qm,qmax))				; значение некорректно
      qm=false(qm)
    endif
    set qm_sys,qm
    set qf_sys,#[vs_sys]                            	; накоп на момент послед сут запом в zond.tmp
    sleep(2)                    		   	; будем польз новым знач QF_SYS
    dout[vi]=0
  endif
  aout[vi+1]=t
endfunc

; переменные
; 1,2 - upd_qyqd sd
; 3,4 - upd_qmes sd
; 5,6 - upd_qmes sf2et1
; 7,8 - upd_qmes sf2et2

; oninit(t)
  ;aout[2]={SVC ВРЕМЯ БЕЛ}        ; чтобы замечать смену суток
  ;aout[4]={SVC ВРЕМЯ БЕЛ}        ; чтобы замечать смену суток
  ;aout[6]={БЕЛБ SF1-TIME}         ; чтобы замечать смену суток
  ;aout[8]={БЕЛБ SF2-TIME}         ; чтобы замечать смену суток
  ;aout[10]={БЕЛБ SF3-TIME}         ; чтобы замечать смену суток

