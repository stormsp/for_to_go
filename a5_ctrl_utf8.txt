; ГРС ДЕСНА
; 02.2022
; управление оборудованием
; valTrack возращает 0 если val равен 0
; или если с момента перехода val в состояние,
; отличное от 0 не прошло timeout секунд.
; В противном случае функция возвращает 1.
;
func valTrack(val,timeout,id)
 if(eq(val,0))
  aout[id]=0
  return(0)
 endif
; aout[id] время перехода в состояние, отличное от 0
; для вычисления тайм-аута (в тиках со старта зонда)
 if(eq(aout[id],0))
  aout[id]=getticks(0)
 endif
 if(ge(getticks(aout[id])*ticksize(),timeout))
  return(1)
 endif
 return(0)
endfunc
;
; управление при условии связи с модулем (для ЭР03)
; sys - системный номер параметра ЭР03
; val - значение
;
func set_er03(sys,val)
  if (eq(dost(#[sys]),1))	; иначе нет связи с модулем
    set sys, val
  endif         
return(0)
endfunc
;
;управление при связи с модулем синхронно
func set_er03s(SYS,VAL)
  if (eq(DOST(#[SYS]),1))
    R=set_wait(SYS,VAL,10)
  endif
return(0)
endfunc
;
func setex(sys,value)
 if(eq(dost(#[sys]),0))
  return(0)
 endif
 set sys,value
 return(1)
endfunc
; управление при условии достоверности
;
func setwex(sys,value,timeout)
  if(eq(dost(#[sys]),0))
    return(0)
  endif
  return(set_wait(sys,value,timeout))
endfunc
; front 1->0
; src - дискр сигнал
; previ - номер переменной слежения
;
func front0(src,previ)
  x=0
  if (dost(src)&ne(src,dout[previ])&eq(src,0))
    x=1
  endif
  dout[previ]=src
  return(x)
endfunc
; front 0->1
; src - дискр сигнал
; previ - номер переменной слежения
;
func front1(src,previ)
  x=0
  if (dost(src)&ne(src,dout[previ])&eq(src,1))
    x=1
  endif
  dout[previ]=src
  return(x)
endfunc
; -------------------- Слив конденсата
; vis,niz - выс,низ уровень конд
; kr_sys - sys крана слива
; tsl - время слива
; n_rez - переменная результата слива (0-норма, 1-таймаут, 2-неиспр дву)
; n_rez+1 - переменная слежения
;  
func sliv(vis,niz,kr_sys,n_rez,tsl)
  if (eq(aout[n_rez+1],0)&eq(vis,1))      ; если не следим и уровень конд высокий
    x=setwex(kr_sys,0,{Т ОЖ КРАН ДЕС})				; откр сброс auma    
    aout[n_rez+1]=getticks(0)			; начали следить
  endif
  if (ne(aout[n_rez+1],0))			; следим
    tas=ge(getticks(aout[n_rez+1])*ticksize(),tsl)
    if (eq(niz,1)|tas)				; ур конд низкий или истекло время  
      x=setwex(kr_sys,1,{Т ОЖ КРАН ДЕС})			; закр сброс auma
      if (vis)
        dout[n_rez]=1				
      else
        dout[n_rez]=tas				; результат слива
      endif
      aout[n_rez+1]=0                           ; не следим
    endif
  endif
endfunc
;-------------------- Включение
; znp - значение ТС загазованности & ! пожар
; V_FB_SYS - сист номер ТС состояния вент
; V_CNTSYS - системный номер выхода ЭР03 на включение вентилятора
; vi - номер переменной слеж
; vi+1 - номер переменной ПС
;
FUNC CHK_VENT_ON(znp,V_FB_SYS,V_CNTSYS,vi)
  if (front1(znp,vi))
   A=SET_ER03S(V_CNTSYS,1)
    SLEEP(5*18)
    if(!#[V_FB_SYS])
      dout[vi+1]=1
    endif
  endif
RETURN(0)
ENDFUNC
;-------------------- Выключение по пожару
; poz - тс пожар
; v_csys - #sys выключения вентилятора (имп)
; vi - переменная для front
;
func chk_vent_off_p(vi,poz,v_csys)
  if (front1(poz,vi))
    x=setex(v_csys,0)
  endif
return(0)
endfunc
;-------------------- Выключение по окончанию загазованности
; zag - загазов
; v_csys - #sys выключения вентилятора (имп)
; vi - переменная для front
;
func chk_vent_off_z(vi,zag,v_csys)
  if (front0(zag,vi))
    x=setex(v_csys,0)
  endif
return(0)
endfunc
; продление сигнала загазованнности при его отключении 
; вент выдул - тс загаз упал, а мы сымитировали, что упал позже
; zag - текущий сигнал загазованности
; vi   - номер переменной слежения dout[vi]
; vi+1 - номер переменной счетчика aout[vi+1]
; T    - задержка в с
; возврат - продленный сигнал загазованности
;
func zaglong(zag,vi,T)
  ret=zag
  if (eq(zag,0)&eq(dout[vi],1))                         
    aout[vi+1]=getticks(0)		; только упал, начинаем отсчет
    ret=1				      ; и имитируем, что еще стоит
  else
    if (ne(aout[vi+1],0))           ; отсчитываем хвост
      if (eq(zag,1))    		; но опять возникла загаз
        aout[vi+1]=getticks(0)      ; сбросил, сшиваем, ret=1
      endif
      a=(getticks(0)-aout[vi+1])*ticksize()
      if (gt(a,T))  			; после пропажи прошло еще время, с
        aout[vi+1]=0			; перестали считать
        ret=0				; опустили сами
      else
        ret=1                       ; держим
      endif
    endif
  endif
  dout[vi]=zag				; следим здесь, нельзя раньше выходить
return(ret)
endfunc
;
; Выходные дискретные переменные:
func oninit(T)
  initouts 1,0,40
  sleep(48)
endfunc
;____________________Включение табло загазованности и пожара
;
; два табло в ББП по СН4
if eq((({1П СГ ЗАГ1 ДЕС})&eq({1П СГ СОСТ ДЕС},0))|(({2П СГ ЗАГ1 ДЕС})&eq({2П СГ СОСТ ДЕС},0))|(({3П СГ ЗАГ1 ДЕС})&eq({3П СГ СОСТ ДЕС},0))|(({4П СГ ЗАГ1 ДЕС})&eq({4П СГ СОСТ ДЕС},0)),1)
   if eq((({1П СГ ЗАГ2 ДЕС})&eq({1П СГ СОСТ ДЕС},0))|(({2П СГ ЗАГ2 ДЕС})&eq({2П СГ СОСТ ДЕС},0))|(({3П СГ ЗАГ2 ДЕС})&eq({3П СГ СОСТ ДЕС},0))|(({4П СГ ЗАГ2 ДЕС})&eq({4П СГ СОСТ ДЕС},0)),1)
   SET {т1ББПвключ ДЕС}[SYS_NUM],2
   x=set_er03s({табл АварияДЕС}[sys_num],1)
   else
   SET {т1ББПвключ ДЕС}[SYS_NUM],1
   x=set_er03s({звонок шк ДЕС}[sys_num],1)
   endif
else
SET {т1ББПвключ ДЕС}[SYS_NUM],0
endif
SET {т2ББПвключ ДЕС}[SYS_NUM],({т1ББПвключ ДЕС})
;
; два табло в ББТ по СН4
if eq((({1Т СГ ЗАГ1 ДЕС})&eq({1Т СГ СОСТ ДЕС},0))|(({2Т СГ ЗАГ1 ДЕС})&eq({2Т СГ СОСТ ДЕС},0))|(({3Т СГ ЗАГ1 ДЕС})&eq({3Т СГ СОСТ ДЕС},0)),1)
   if eq((({1Т СГ ЗАГ2 ДЕС})&eq({1Т СГ СОСТ ДЕС},0))|(({2Т СГ ЗАГ2 ДЕС})&eq({2Т СГ СОСТ ДЕС},0))|(({3Т СГ ЗАГ2 ДЕС})&eq({3Т СГ СОСТ ДЕС},0)),1)
   SET {т1ББТвключ ДЕС}[SYS_NUM],2
   x=set_er03s({табл АварияДЕС}[sys_num],1)
   else
   SET {т1ББТвключ ДЕС}[SYS_NUM],1
   x=set_er03s({звонок шк ДЕС}[sys_num],1)
   endif
else
SET {т1ББТвключ ДЕС}[SYS_NUM],0
endif
SET {т2ББТвключ ДЕС}[SYS_NUM],({т1ББТвключ ДЕС})
;
; два табло в котельной + по угарному газу
if eq((({1К СГ ЗАГ1 ДЕС})&eq({1К СГ СОСТ ДЕС},0))|(({2К СГ ЗАГ1 ДЕС})&eq({2К СГ СОСТ ДЕС},0))|(({1СС ЗАГ 1П ДЕС})&eq({1СС ДЕС},0))|(({1СС ЗАГ 1П ДЕС})&eq({1СС ДЕС},0)),1)
   if eq((({1К СГ ЗАГ2 ДЕС})&eq({1К СГ СОСТ ДЕС},0))|(({2К СГ ЗАГ2 ДЕС})&eq({2К СГ СОСТ ДЕС},0))|(({1СС ЗАГ 2П ДЕС})&eq({1СС ДЕС},0)),1)
   SET {т1КОТвключ ДЕС}[SYS_NUM],2
   x=set_er03s({табл АварияДЕС}[sys_num],1)
   else
   SET {т1КОТвключ ДЕС}[SYS_NUM],1
   x=set_er03s({звонок шк ДЕС}[sys_num],1)
   endif
else
SET {т1КОТвключ ДЕС}[SYS_NUM],0
endif
SET {т2КОТвключ ДЕС}[SYS_NUM],({т1КОТвключ ДЕС});
;
; табло СН4 в Одоризаторной
if eq((({1О СГ ЗАГ1 ДЕС})&eq({1О СГ СОСТ ДЕС},0))|(({2О СГ ЗАГ1 ДЕС})&eq({2О СГ СОСТ ДЕС},0)),1)
   if eq((({1О СГ ЗАГ2 ДЕС})&eq({1О СГ СОСТ ДЕС},0))|(({2О СГ ЗАГ2 ДЕС})&eq({2О СГ СОСТ ДЕС},0)),1)
   SET {тУзОдвключ ДЕС}[SYS_NUM],2
   x=set_er03s({табл АварияДЕС}[sys_num],1)
   else
   SET {тУзОдвключ ДЕС}[SYS_NUM],1
   x=set_er03s({звонок шк ДЕС}[sys_num],1)
   endif
else
SET {тУзОдвключ ДЕС}[SYS_NUM],0
endif
;
;______________________включение вентилятора______________________
;
if eq({РЕЖИМ ГРС ДЕС},3)
; вкл по загазованности ПОРОГ 1 или по кнопке
    z1=(({1П СГ ЗАГ1 ДЕС})&eq({1П СГ СОСТ ДЕС},0))|(({2П СГ ЗАГ1 ДЕС})&eq({2П СГ СОСТ ДЕС},0))|(({3П СГ ЗАГ1 ДЕС})&eq({3П СГ СОСТ ДЕС},0))|(({4П СГ ЗАГ1 ДЕС})&eq({4П СГ СОСТ ДЕС},0))
    A=CHK_VENT_ON((z1|{ВенББП кнопДЕС})&!{Пожар ГРС ДЕС},{ВенББП состДЕС}[sys_num],{Вент БлПер ДЕС}[sys_num],1) 
    
    z2=(({1Т СГ ЗАГ1 ДЕС})&eq({1Т СГ СОСТ ДЕС},0))|(({2Т СГ ЗАГ1 ДЕС})&eq({2Т СГ СОСТ ДЕС},0))|(({3Т СГ ЗАГ1 ДЕС})&eq({3Т СГ СОСТ ДЕС},0))
    A=CHK_VENT_ON((z2|{ВенББТ кнопДЕС})&!{Пожар ГРС ДЕС},{ВенББТ состДЕС}[sys_num],{Вент БлТехнДЕС}[sys_num],3) 
    
    z3=(({1К СГ ЗАГ1 ДЕС})&eq({1К СГ СОСТ ДЕС},0))|(({2К СГ ЗАГ1 ДЕС})&eq({2К СГ СОСТ ДЕС},0)|({1СС ЗАГ 1П ДЕС})&eq({1СС ДЕС},0))
    A=CHK_VENT_ON((z3|{ВенКот кнопДЕС})&!{Пожар ГРС ДЕС},{ВенКот состДЕС}[sys_num],{Вент котельДЕС}[sys_num],5) 
    
    z4=(({1О СГ ЗАГ1 ДЕС})&eq({1О СГ СОСТ ДЕС},0))|(({2О СГ ЗАГ1 ДЕС})&eq({2О СГ СОСТ ДЕС},0))
    A=CHK_VENT_ON((z4|{ВенУзО кнопДЕС})&!{Пожар ГРС ДЕС},{ВенУзО состДЕС}[sys_num],{Вент БлОдорДЕС}[sys_num],7) 
;
; выкл по пожару
;
  x=chk_vent_off_p(9,{Пожар ГРС ДЕС},{Вент БлПер ДЕС}[sys_num]) 	
  x=chk_vent_off_p(10,{Пожар ГРС ДЕС},{Вент БлТехнДЕС}[sys_num]) 	
  x=chk_vent_off_p(11,{Пожар ГРС ДЕС},{Вент котельДЕС}[sys_num]) 	
  x=chk_vent_off_p(12,{Пожар ГРС ДЕС},{Вент БлОдорДЕС}[sys_num]) 	
;  
; выкл по окончанию загазованности 
;  
  x=chk_vent_off_z(13,zaglong(z1,15,10),{Вент БлПер ДЕС}[sys_num])	
  x=chk_vent_off_z(16,zaglong(z2,17,60),{Вент БлТехнДЕС}[sys_num])	
  x=chk_vent_off_z(19,zaglong(z3,20,60),{Вент котельДЕС}[sys_num])	
  x=chk_vent_off_z(22,zaglong(z4,23,60),{Вент БлОдорДЕС}[sys_num])	
endif
;
;____________________блокировка котлов отсека ПТН
;
if eq({авар ОПТ ДЕС}|{прор ПГ3.1 ДЕС}|{прор ПГ3.2 ДЕС}|(({1К СГ ЗАГ1 ДЕС})&eq({1К СГ СОСТ ДЕС},0))|(({2К СГ ЗАГ1 ДЕС})&eq({2К СГ СОСТ ДЕС},0))|(({1СС ЗАГ 1П ДЕС})&eq({1СС ДЕС},0))|(({1СС ЗАГ 1П ДЕС})&eq({1СС ДЕС},0)),1)
  x=set_er03s({звонок шк ДЕС}[sys_num],1)
  x=set_er03s({Клап котлы ДЕС}[sys_num],1)
  x=set_er03s({ТУ КОТЕЛ 1 ДЕС}[sys_num],1)
  x=set_er03s({ТУ КОТЕЛ 2 ДЕС}[sys_num],1)
  x=set_er03s({ТУ НАС К1 ДЕС}[sys_num],0)
  x=set_er03s({ТУ НАС К2 ДЕС}[sys_num],0)
  x=set_er03s({ТУ НАСПГ1 ДЕС}[sys_num],0)
  x=set_er03s({ТУ НАСПГ2 ДЕС}[sys_num],0)
  x=set_er03s({ТУ НАСОТ1 ДЕС}[sys_num],0)
  x=set_er03s({ТУ НАСОТ2 ДЕС}[sys_num],0)
  x=set_er03s({ТУ НАСПОДП ДЕС}[sys_num],0)
  dout[33]=1 ;сигнал блок упр насосами
endif
;
; -----------------Слив конденсата---------------------- 
;
; если разрешен слив 
if (eq({РЕЖИМ ГРС ДЕС},3)&eq({ХОД АО СТ ДЕС},0)&eq({ХОД АО БС ДЕС},0)&!{РЗР СЛИВ ДЕС})
  if eq({LкондФ1 ВР ДЕС},1) ; уровень высокий
  x=setwex({СлФС1 ДЕС}[sys_num],0,{Т ож кран ДЕС}) ; открыть кран сброса
  sleep (50) ; пауза
     if eq ({LкондФ1 ВР ДЕС}&{LкондФ1 НР ДЕС},1) ; проверка нет падения уровня
     set {РЗР СЛИВ ДЕС}[sys_num],1 ; снятие авт режима слива 
     x=set_er03s({звонок шк ДЕС}[sys_num],1)
     endif                                                            
  endif
endif
;
if eq({LкондФ1 НР ДЕС},1) ; уровень низкий
  x=setwex({СлФС1 ДЕС}[sys_num],1,{Т ож кран ДЕС}) ; закрыть кран сброса
endif
;
if(eq({РЕЖИМ ГРС ДЕС},3)&eq({ХОД АО СТ ДЕС},0)&eq({ХОД АО БС ДЕС},0)&!{РЗР СЛИВ ДЕС})
  if eq({LкондФ2 ВР ДЕС},1)  
  x=setwex({СлФС2 ДЕС}[sys_num],0,{Т ож кран ДЕС})
  sleep (50)
     if eq ({LкондФ2 ВР ДЕС}&{LкондФ2 НР ДЕС},1)
     set {РЗР СЛИВ ДЕС}[sys_num],1 ; снятие авт режима слива 
     x=set_er03s({звонок шк ДЕС}[sys_num],1) 
     endif
   endif
endif
;
if eq({LкондФ2 НР ДЕС},1)
  x=setwex({СлФС2 ДЕС}[sys_num],1,{Т ож кран ДЕС}) ; закрыть кран сброса
endif
;
;
;----------------Переключение насосов-------------------
;
if eq({РЕЖИМ ГРС ДЕС},3)&!{РЗР насос ДЕС}&!{БЛОК НАС ДЕС}
  if front1(eq(hour({Время ДЕС}),{ЧАС НАСОС ДЕС}),39)
      if !({ТС НАСПГ1 ДЕС})&({ТС НАСПГ2 ДЕС})  ; насос1 в работе и насос2 отключен
      set {ТУ НАСПГ2 ДЕС}[sys_num],1         ; включить насос 2
      set {ТУ НАСПГ1 ДЕС}[sys_num],0         ; отключить насос 1
      else
        if !({ТС НАСПГ2 ДЕС})&({ТС НАСПГ1 ДЕС})   ;насос2 в работе и насос1 отключен
        set {ТУ НАСПГ1 ДЕС}[sys_num],1         ; включить насос 1
        set {ТУ НАСПГ2 ДЕС}[sys_num],0         ; отключить насос 2
        endif
      endif
    if !({ТС НАСОТ1 ДЕС})&({ТС НАСОТ2 ДЕС})    ; насос1 в работе и насос2 отключен
      set {ТУ НАСОТ2 ДЕС}[sys_num],1         ; включить насос 2
      set {ТУ НАСОТ1 ДЕС}[sys_num],0         ; отключить насос 1
    else
      if !({ТС НАСОТ2 ДЕС})&({ТС НАСОТ1 ДЕС})  ;насос2 в работе и насос1 отключен
      set {ТУ НАСОТ1 ДЕС}[sys_num],1         ; включить насос 1
      set {ТУ НАСОТ2 ДЕС}[sys_num],0         ; отключить насос 2
      endif
    endif    
  endif
endif
;
;----------------управление подпиточным насосом-------------------
;г
if le({Ртн кон подДЕС},0.24)                ; давление в контуре подпитки мало
   set {ТУ НАСПОДП ДЕС}[sys_num],1          ; включить насос
endif
;
if ge({Ртн кон подДЕС},0.3)                 ; давление в контуре подпитки выросло
   set {ТУ НАСПОДП ДЕС}[sys_num],0          ; выключить насос
endif
;
if eq({НАСПОДП ДЕС},1)                      ; при сигнале аварии насоса
   set {ТУ НАСПОДП ДЕС}[sys_num],0          ; выключить насос
   x=set_er03s({звонок шк ДЕС}[sys_num],1)  ; включить ПС
endif

